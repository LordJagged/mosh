use crate::gc::Gc;
use crate::lexer;
use crate::numbers;
use crate::numbers::exact;
use crate::numbers::inexact;
use crate::numbers::Compnum;
use crate::numbers::Flonum;
use crate::numbers::Ratnum;
use crate::error::SchemeError;
use crate::reader_util::read_uinteger2;
use crate::reader_util::read_uinteger8;
use crate::reader_util::read_uinteger10;
use crate::reader_util::read_uinteger16;
use crate::objects::Object;
use std::str::FromStr;
use num_bigint::BigInt;
use num_traits::Num;
use regex::Regex;
use lalrpop_util::ParseError;
use num_traits::FromPrimitive;

grammar(gc: &mut Box<Gc>, is_inexact_context: &mut bool);

// R7RS small 7.1.2. External representations
pub Number: Object = {
    Number10,
    Number16,
    Number2,
    Number8,
}

Number10: Object = {
    <e: Prefix10?> <c: Complex10> => {
        match e {
            Some(Some(is_exact)) => {
                if is_exact {
                    exact(gc, c)
                } else {
                    inexact(gc, c)
                }
            },
            Some(None) | None => {
                // To handle #e1e1000.
                // See below in decimal10 section.
                if *is_inexact_context {
                    inexact(gc, c)
                } else {
                    c
                }
            },
        }
    },
}

Number16: Object = {
    <e: Prefix16> <c: Complex16> => {
        match e {
            Some(is_exact) => {
                if is_exact {
                    exact(gc, c)
                } else {
                    inexact(gc, c)
                }
            },
            None => {
                c
            }
        }
    }
}

Number8: Object = {
    <e: Prefix8> <c: Complex8> => {
        match e {
            Some(is_exact) => {
                if is_exact {
                    exact(gc, c)
                } else {
                    inexact(gc, c)
                }
            },
            None => {
                c
            }
        }
    }
}

Number2: Object = {
    <e: Prefix2> <c: Complex2> => {
        match e {
            Some(is_exact) => {
                if is_exact {
                    exact(gc, c)
                } else {
                    inexact(gc, c)
                }
            },
            None => {
                c
            }
        }
    }
}

Prefix10: Option<bool> = {
    Radix10 <e: Exactness> => Some(e),
    <e: Exactness> Radix10 => Some(e),
    Radix10 => None,
    <e: Exactness> => Some(e),
}

Prefix8: Option<bool> = {
    Radix8 <e: Exactness?> => {
        match e {
            Some(is_exact) => Some(is_exact),
            None => None,
        }
    },
    <is_exact: Exactness> Radix8 => Some(is_exact),
}

Prefix2: Option<bool> = {
    Radix2 <e: Exactness?> => {
        match e {
            Some(is_exact) => Some(is_exact),
            None => None,
        }
    },
    <is_exact: Exactness> Radix2 => Some(is_exact),
}

Prefix16: Option<bool> = {
    Radix16 <e: Exactness?> => {
        match e {
            Some(is_exact) => Some(is_exact),
            None => None,
        }
    },
    <is_exact: Exactness> Radix16 => Some(is_exact),
}

Exactness: bool = {
    "#e" => true,
    "#i" => false,
}

Radix10 = {
    "#d"
}

Radix8 = {
    "#o"
}

Radix2 = {
    "#b"
}

Radix16 = {
    "#x"
}

Complex10: Object = {
    Real10,
    <lhs: Real10> "@" <rhs: Real10> => {
        numbers::make_polar(gc, lhs, rhs)
    },    
    <real: Real10> "+" <imag: UReal10> "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, imag)))
    },
    <real: Real10> "-" <imag: UReal10> "i" => {
        let imag = imag.neg(gc);
        Object::Compnum(gc.alloc(Compnum::new(real, imag)))
    },
    <real: Real10> "+" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Fixnum(1))))
    },
    <real: Real10> "+inf.0" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Flonum(Flonum::new(f64:: INFINITY)))))
    },
    <real: Real10> "-inf.0" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Flonum(Flonum::new(f64:: NEG_INFINITY)))))
    },
    "+inf.0" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(Object::Fixnum(0), Object::Flonum(Flonum::new(f64:: INFINITY)))))
    },
    "-inf.0" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(Object::Fixnum(0), Object::Flonum(Flonum::new(f64:: NEG_INFINITY)))))
    },    
    <real: Real10> "-" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Fixnum(-1))))
    },
    "+" <imag: Real10> "i" => {
        Object::Compnum(gc.alloc(Compnum::new(Object::Fixnum(0), imag)))
    },
    "-" <imag: Real10> "i" => {
        let imag = imag.neg(gc);
        Object::Compnum(gc.alloc(Compnum::new(Object::Fixnum(0), imag)))
    },
    "+" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(Object::Fixnum(0), Object::Fixnum(1))))
    },
    "-" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(Object::Fixnum(0), Object::Fixnum(-1))))
    },
}

Complex8: Object = {
    Real8,
    <lhs: Real8> "@" <rhs: Real8> => {
        numbers::make_polar(gc, lhs, rhs)
    },        
    <real: Real8> "+" <imag: UReal8> "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, imag)))
    },
    <real: Real8> "-" <imag: UReal8> "i" => {
        let imag = imag.neg(gc);
        Object::Compnum(gc.alloc(Compnum::new(real, imag)))
    },
    <real: Real8> "+" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Fixnum(1))))
    },
    <real: Real8> "+inf.0" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Flonum(Flonum::new(f64:: INFINITY)))))
    },
    <real: Real8> "-" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Fixnum(-1))))
    },
}

Complex2: Object = {
    Real2,
    <lhs: Real2> "@" <rhs: Real2> => {
        numbers::make_polar(gc, lhs, rhs)
    },        
    <real: Real2> "+" <imag: UReal2> "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, imag)))
    },
    <real: Real2> "-" <imag: UReal2> "i" => {
        let imag = imag.neg(gc);
        Object::Compnum(gc.alloc(Compnum::new(real, imag)))
    },
    <real: Real2> "+" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Fixnum(1))))
    },
    <real: Real2> "+inf.0" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Flonum(Flonum::new(f64:: INFINITY)))))
    },
    <real: Real2> "-" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Fixnum(-1))))
    },
}

Complex16: Object = {
    Real16,
    <lhs: Real16> "@" <rhs: Real16> => {
        numbers::make_polar(gc, lhs, rhs)
    },        
    <real: Real16> "+" <imag: UReal16> "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, imag)))
    },
    <real: Real16> "-" <imag: UReal16> "i" => {
        let imag = imag.neg(gc);
        Object::Compnum(gc.alloc(Compnum::new(real, imag)))
    },
    <real: Real16> "+" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Fixnum(1))))
    },
    <real: Real16> "+inf.0" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Flonum(Flonum::new(f64:: INFINITY)))))
    },
    <real: Real16> "-" "i" => {
        Object::Compnum(gc.alloc(Compnum::new(real, Object::Fixnum(-1))))
    },
}

Real10: Object = {
    UReal10,
    "+" <ureal10: UReal10> => {
        ureal10
    },
    "-" <ureal10: UReal10> => {
        ureal10.neg(gc)
    },
    InfNan,
}

Real8: Object = {
    UReal8,
    "+" <ureal8: UReal8> => {
        ureal8
    },
    "-" <ureal8: UReal8> => {
        ureal8.neg(gc)
    },
}

Real2: Object = {
    UReal2,
    "+" <ureal2: UReal2> => {
        ureal2
    },
    "-" <ureal2: UReal2> => {
        ureal2.neg(gc)
    },
}

Real16: Object = {
    UReal16,
    "+" <ureal16: UReal16> => {
        ureal16
    },
    "-" <ureal16: UReal16> => {
        ureal16.neg(gc)
    },
}

UReal10: Object = {
    UInteger10 =>? {
        match read_uinteger10(gc, &<>) {
            Ok(obj) => Ok(obj),
            Err(err) => {
                Err(ParseError::User { error: err })
            }
        }
    },
    <numer: UInteger10> "/" <denom: UInteger10> =>? {
        match (BigInt::from_str(&numer), BigInt::from_str(&denom)) {
            (Ok(n), Ok(d)) => {
                if d == BigInt::from_isize(0).unwrap() {
                    Err(ParseError::User {
                        error: SchemeError::division_by_zero("read", &format!("number={} denom={}", n, d), &[])
                    })
                } else {
                    Ok(Object::Ratnum(gc.alloc(Ratnum::new(n, d))))
                }
            }
            (Err(err), _) => {
                Err(ParseError::User {
                    error: SchemeError::lexical_violation_read_error("read", &format!("number parse error: {:?}: token: {}", err, numer))
                })
            }
            (_, Err(err)) => {
                Err(ParseError::User {
                    error: SchemeError::lexical_violation_read_error("read", &format!("number parse error: {:?} token: {}", err, denom))
                })
            }
        }
    },
    Decimal10 =>? {
        // To handle #e1e1000 case we parse 1e1000 style as exact.
        // Then later makes them inexact if there's not #e prefix.
        let re = Regex::new(r"^(\d+)e(\-?\d+)$").unwrap();
        if let Some(captures) = re.captures(&<>) {
            match (isize::from_str(&captures[1]), isize::from_str(&captures[2])) {
                (Ok(lhs), Ok(rhs)) => {
                    *is_inexact_context = true;
                    let x = numbers::expt(gc, Object::Fixnum(10), Object::Fixnum(rhs));
                    return Ok(numbers::mul(gc, Object::Fixnum(lhs), x))
                }
                _ => { /* fall through */ }
            }
        }
        match f64::from_str(&<>) {
            Ok(n) => {
                Ok(Object::Flonum(Flonum::new(n)))
            }
            Err(err) => {
                Err(ParseError::User {
                    error: SchemeError::lexical_violation_read_error("reader", &format!("number parse error: {:?} token: {}", err, <>))
                })
            }
        }
    }
}

UReal16: Object = {
    UInteger16 =>? {
        match read_uinteger16(gc, &<>) {
            Ok(obj) => Ok(obj),
            Err(err) => {
                Err(ParseError::User {
                    error: SchemeError::lexical_violation_read_error("read", &format!("number parse error: {:?} token: {}", err, <>))
                })
            }
        }
    },
    <numer: UInteger16> "/" <denom: UInteger16> =>? {
        match (BigInt::from_str_radix(&numer, 16), BigInt::from_str_radix(&denom, 16)) {
            (Ok(n), Ok(d)) => {
                if d == BigInt::from_isize(0).unwrap() {
                    Err(ParseError::User {
                        error: SchemeError::division_by_zero("read", &format!("number={} denom={}", n, d), &[])
                    })
                } else {
                    Ok(Object::Ratnum(gc.alloc(Ratnum::new(n, d))))
                }
            }
            (Err(err), _) => {
                Err(ParseError::User {
                    error: SchemeError::lexical_violation_read_error("read", &format!("number parse error: {:?} token: {}", err, numer))
                })
            }
            (_, Err(err)) => {
                Err(ParseError::User {
                    error: SchemeError::lexical_violation_read_error("read", &format!("number parse error: {:?} token: {}", err, denom))
                })
            }
        }
    },
}

UReal8: Object = {
    UInteger8 =>? {
        match read_uinteger8(gc, &<>) {
            Ok(obj) => Ok(obj),
            Err(err) => {
                Err(ParseError::User {
                    error: SchemeError::lexical_violation_read_error("read", &format!("number parse error: {:?} token: {}", err, <>))
                })
            }
        }
    },
    <numer: UInteger8> "/" <denom: UInteger8> =>? {
        match (BigInt::from_str_radix(&numer, 8), BigInt::from_str_radix(&denom, 8)) {
            (Ok(n), Ok(d)) => {
                if d == BigInt::from_isize(0).unwrap() {
                    Err(ParseError::User {
                        error: SchemeError::division_by_zero("read", &format!("numer={} denom={}", n, d), &[])
                    })
                } else {
                    Ok(Object::Ratnum(gc.alloc(Ratnum::new(n, d))))
                }
            }
            (Err(err), _) => {
                Err(ParseError::User {
                    error:  SchemeError::lexical_violation_read_error("read", &format!("number parse error: {:?} token: {}", err, numer))
                })
            }
            (_, Err(err)) => {
                Err(ParseError::User {
                    error: SchemeError::lexical_violation_read_error("read", &format!("number parse error: {:?} token: {}", err, denom))
                })
            }
        }
    },
}

UReal2: Object = {
    UInteger2 =>? {
        match read_uinteger2(gc, &<>) {
            Ok(obj) => Ok(obj),
            Err(err) => {
                Err(ParseError::User {
                    error: SchemeError::lexical_violation_read_error("read", &format!("number parse error: {:?} token: {}", err, <>))
                })
            }
        }
    },
    <numer: UInteger2> "/" <denom: UInteger2> =>? {
        match (BigInt::from_str_radix(&numer, 2), BigInt::from_str_radix(&denom, 2)) {
            (Ok(n), Ok(d)) => {
                if d == BigInt::from_isize(0).unwrap() {
                    Err(ParseError::User {
                        error: SchemeError::division_by_zero("read", &format!("numer={} denom={}", n, d), &[])
                    })
                } else {
                    Ok(Object::Ratnum(gc.alloc(Ratnum::new(n, d))))
                }
            }
            (Err(err), _) => {
                Err(ParseError::User {
                    error: SchemeError::lexical_violation_read_error("read", &format!("number parse error: {:?} token: {}", err, numer))
                })
            }
            (_, Err(err)) => {
                Err(ParseError::User {
                    error: SchemeError::lexical_violation_read_error("read", &format!("number parse error: {:?} token: {}", err, denom))
                })
            }
        }
    },
}

Decimal10: String = {
    <uinteger10: UInteger10> <suffix: Suffix> => {
        uinteger10 + &suffix
    },
    "." <digits:  Digit10 +> <suffix: Suffix ?> => {
        ".".to_owned() + &digits.join("") + &suffix.unwrap_or("".to_string())
    },
    <digits:  Digit10 +> "." <digits2:  Digit10 *> <suffix: Suffix ?> => {
        digits.join("") + &"." + &digits2.join("") + &suffix.unwrap_or("".to_string())
    },
}

Suffix: String = {
    Exponent,
}

UInteger10: String = {
    <digits: Digit10 +> => {
        digits.join("")
    }
}

UInteger8: String = {
    <digits: Digit8 +> => {
        digits.join("")
    },
}

UInteger2: String = {
    <digits: Digit2 +> => {
        digits.join("")
    },
}

UInteger16: String = {
    <digits: Digit16 +> => {
        digits.join("")
    },
}

InfNan: Object = {
    "+inf.0" => {
        Object::Flonum(Flonum::new(f64:: INFINITY))
    },
    "-inf.0" => {
        Object::Flonum(Flonum::new(f64:: NEG_INFINITY))
    },
    "+nan.0" => {
        Object::Flonum(Flonum::new(f64:: NAN))
    },
    "-nan.0" => {
        Object::Flonum(Flonum::new(f64:: NAN))
    },
}

Exponent: String = {
    <e:"exponent"> => {
        e.to_string()
    }
}


Digit10: String = {
    <s:"digit"> => {
        s.to_string()
    }
}

Digit8: String = {
    <s:"oct_digit"> => {
        s.to_string()
    }
}

Digit2: String = {
    <s:"bin_digit"> => {
        s.to_string()
    }
}

Digit16: String = {
    <s:"hex_digit"> => {
        s.to_string()
    },
}

extern {
    type Location = usize;
    type Error = SchemeError;

    enum lexer::Token {
        "-" => lexer::Token::Minus,
        "." => lexer::Token::Dot,
        "/" => lexer::Token::Slash,
        "#d" => lexer::Token::Radix10,
        "#b" => lexer::Token::Radix2,
        "#o" => lexer::Token::Radix8,
        "#x" => lexer::Token::Radix16,
        "#e" => lexer::Token::Exact,
        "#i" => lexer::Token::Inexact,
        "+" => lexer::Token::Plus,
        "@" => lexer::Token::At,
        "digit" => lexer::Token::Digit { value: <String> },
        "bin_digit" => lexer::Token::BinDigit { value: <String> },
        "oct_digit" => lexer::Token::OctDigit { value: <String> },
        "hex_digit" => lexer::Token::HexDigit { value: <String> },
        "exponent" => lexer::Token::Exponent { value: <String> },
        "i" => lexer::Token::Imag,
        "+inf.0" => lexer::Token::PlusInf,
        "-inf.0" => lexer::Token::MinusInf,
        "+nan.0" => lexer::Token::PlusNan,
        "-nan.0" => lexer::Token::MinusNan,
    }
}